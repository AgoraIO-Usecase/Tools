apply plugin: 'com.android.library'

def safeExtGet(prop, key, fallback) {
    if (rootProject.ext.has(prop)) {
        def map = rootProject.ext.get(prop)
        if (map.containsKey(key)) {
            return map.get(key)
        }
    }
    return fallback
}

// add the maven publish script if existed, otherwise use the github upload script
def maven_gradle_path = "${rootDir.absolutePath}/maven.gradle"
if (new File(maven_gradle_path).exists()) {
    apply from: maven_gradle_path
}else{
    apply plugin: 'com.github.dcendents.android-maven'
}

// special the group name and version using when publishing to maven
project.group = 'io.github.agoraio-usecase.tools'
project.version = "${safeExtGet('agora_scene_statistic', 'versionName', "1.0.1")}"
project.description = "AgoraSceneStatistic"
project.ext.url = "https://github.com/AgoraIO-Usecase/Tools/tree/dev/android_statistic/AgoraSceneStatistic-Android"

android {
    compileSdkVersion safeExtGet('android', 'compileSdkVersion', 29)
    buildToolsVersion safeExtGet('android', 'buildToolsVersion', '29.0.3')

    defaultConfig {
        minSdkVersion safeExtGet('android', 'minSdkVersion', 19)
        targetSdkVersion safeExtGet('android', 'minSdkVersion', 29)
        versionCode safeExtGet('agora_scene_statistic', 'versionCode', 1)
        versionName "${project.version}"

        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "androidx.annotation:annotation:${safeExtGet('dependencies', 'androidx.annotation:annotation', '1.1.0')}"
    implementation "com.squareup.retrofit2:retrofit:${safeExtGet('dependencies', 'com.squareup.retrofit2:retrofit', '2.9.0')}"
    implementation "com.squareup.retrofit2:converter-gson:${safeExtGet('dependencies', 'com.squareup.retrofit2:converter-gson', '2.9.0')}"
}
